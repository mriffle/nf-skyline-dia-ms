/**
 * # Parameters for nf-maccoss-trex
 *
 * A NextFlow pipeline for analyzing data-ind
 */
params {
    /** \group{Input/Output Parameters} */

    // the data to be quantified
    quant_spectra_dir = null      // directory containing the raw data
    quant_spectra_glob = '*.raw'  // which files in this directory to use

    // the data to be used to generate a chromatogram library (e.g. narrow window GPF data)
    chromatogram_library_spectra_dir = null     // directory containing the raw data
    chromatogram_library_spectra_glob = '*.raw' // which files in this directory to use

    spectral_library = null                 // can be blib, dlib, or elib
    fasta = null                            // background FASTA

    // The final skyline document will be named using this name. For example,
    // if skyline_custom_name = 'human_dia' then the final Skyline document
    // will be named "human_dia.sky.zip". When importing into PanoramaWeb--this
    // name will appear in the table of imported Skyline documents.
    skyline_document_name = 'final'

    // the search engine to use: must be either 'encyclopedia' or 'diann'
    search_engine = 'encyclopedia'

    // default parameters for Encyclopedia searches, can be overridden by users
    // todo: follow up on these options: -quantifyAcrossSamples true -scoringBreadthType window
    encyclopedia.quant.params           = '-enableAdvancedOptions -v2scoring'
    encyclopedia.chromatogram.params    = '-enableAdvancedOptions -v2scoring'

    // default parameters for DIA-NN
    diann.params = '--unimod4 --qvalue 0.01 --cut \'K*,R*,!*P\' --reanalyse --smart-profiling'

    // whether or not to save the output from encyclopedia running on individual raw/mzML
    // files (e.g. .dia or .features.txt files) to the results directory
    // setting to false can save considerable local disk space if running on AWS Batch
    // the generated chromatogram library (elib) will always be saved, regardless of this setting
    encyclopedia.save_output            = true

    // optional user-supplied parameters
    email = null                    // email to notify of workflow outcome, leave null to send no email
    skyline_template_file = null    // the skyline template, if null use default_skyline_template_file
    panorama_upload = false;        // whether or not to upload the results to PanoramaWeb
    msconvert.do_demultiplex = true;          // whether or not to demultiplex with msconvert
    msconvert.do_simasspectra = true;         // whether or not to do simAsSpectra with msconvert

    // If set to true, only run msconvert and stop. Resulting mzML files will be saved to the
    // "msconvert" subdirectory of the results directory.
    msconvert_only = false

    // Parameters related to uploading results to PanoramaWeb
    panorama.upload = false
    panorama.upload_url = null

    // Skip creating a Skyline document
    skip_skyline = false

    // Set to import a Skyline .skyr file and run any included reports at the end of the workflow
    skyline_skyr_file = null

    // general workflow params, can be changed
    result_dir = 'results/nf-teirex-dia' /** \type{str} Where results will be saved. */
    report_dir = 'reports/nf-teirex-dia' /** \type{str} Where results will be saved. */

    // use this if no user-supplied skyline template file -- suitable for EncyclopeDIA and DiaNN
    default_skyline_template_file = 'https://github.com/mriffle/nf-teirex-dia/raw/main/resources/template.sky.zip'
}

plugins {
    id 'nf-amazon'
}

docker {
    enabled = true
}

/*
 * Set up secrets in the environment. 
 * Need to do it this way because Nextflow doesn't allow the use of secrets when running on AWS Batch
 */

// ensure PanoramaWeb API keys will work. see: https://github.com/nextflow-io/nextflow/issues/3690
env.PLACEHOLDER = "PLACEHOLDER_VALUE"

secret_value = nextflow.secret.SecretsLoader.instance.load().getSecret("PANORAMA_API_KEY")
if(secret_value) {
    env.PANORAMA_API_KEY = secret_value.value
}

aws {

    batch {
        // NOTE: this setting is only required if the AWS CLI tool is installed in a custom AMI
        cliPath = '/usr/local/aws-cli/v2/current/bin/aws'
        logsGroup = '/batch/tei-nextflow-batch'
        maxConnections = 20
        connectionTimeout = 10000
        uploadStorageClass = 'INTELLIGENT_TIERING'
        storageEncryption = 'AES256'
        retryMode = 'standard'
    }

    region = 'us-west-2'
}

// Execution Profiles
profiles {

    /*
     * Params for running pipeline on the local computer (e.g.:
     * your laptop). These can be overridden in the local config file.
     */
    standard {
        process.executor = 'local'

        // limit nextflow to running 1 task at a time
        executor.queueSize = 1

        params.max_memory = '12.GB'
        params.max_cpus = 8
        params.max_time = '240.h'

        // where to cache mzml files after running msconvert
        params.mzml_cache_directory = '/data/mass_spec/nextflow/nf-teirex-dia/mzml_cache'
        params.panorama_cache_directory = '/data/mass_spec/nextflow/panorama/raw_cache'
    }

    aws {
        process.executor = 'awsbatch'
        process.queue = 'nextflow_basic_ec2'

        // params for running pipeline on aws batch
        // These can be overridden in local config file

        // max params allowed for your AWS Batch compute environment
        params.max_memory = '124.GB'
        params.max_cpus = 32
        params.max_time = '240.h'

        // where to cache mzml files after running msconvert
        params.mzml_cache_directory = 's3://mc-tei-rex-nextflow-dda/dia/mzml_cache'
        params.panorama_cache_directory = 's3://mc-tei-rex-nextflow-dda/panorama_cache'
    }

    slurm {
        process.executor = 'slurm'

        params.max_memory = '12.GB'
        params.max_cpus = 8
        params.max_time = '240.h'

        // where to cache mzml files after running msconvert
        params.mzml_cache_directory = '/data/mass_spec/nextflow/nf-teirex-dia/mzml_cache'
        params.panorama_cache_directory = '/data/mass_spec/nextflow/panorama/raw_cache'
    }

}

// Manifest
manifest {
    name            = 'nf-teirex-dia'
    author          = 'Michael Riffle'
    homePage        = 'https://github.com/mriffle/nf-teirex-dia'
    description     = 'DIA workflows for TEI-REX project'
    mainScript      = 'main.nf'
    nextflowVersion = '!>=21.10.3'
}

// Capture exit codes from upstream processes when piping
process.shell = ['/bin/bash', '-euo', 'pipefail']
def trace_timestamp = new java.util.Date().format( 'yyyy-MM-dd_HH-mm-ss')
timeline {
    enabled = true
    file    = "${params.report_dir}/execution_timeline_${trace_timestamp}.html"
}
report {
    enabled = true
    file    = "${params.report_dir}/execution_report_${trace_timestamp}.html"
}
trace {
    enabled = true
    file    = "${params.report_dir}/execution_trace_${trace_timestamp}.txt"
}
dag {
    enabled = false
    file    = "${params.report_dir}/pipeline_dag_${trace_timestamp}.html"
}

// Load base.config by default for all pipelines
includeConfig 'conf/base.config'

// Function to ensure that resource requirements don't go beyond
// a maximum limit. Copied from the nf-core template.
def check_max(obj, type) {
    if (type == 'memory') {
        try {
            if (obj.compareTo(params.max_memory as nextflow.util.MemoryUnit) == 1)
                return params.max_memory as nextflow.util.MemoryUnit
            else
                return obj
        } catch (all) {
            println "   ### ERROR ###   Max memory '${params.max_memory}' is not valid! Using default value: $obj"
            return obj
        }
    } else if (type == 'time') {
        try {
            if (obj.compareTo(params.max_time as nextflow.util.Duration) == 1)
                return params.max_time as nextflow.util.Duration
            else
                return obj
        } catch (all) {
            println "   ### ERROR ###   Max time '${params.max_time}' is not valid! Using default value: $obj"
            return obj
        }
    } else if (type == 'cpus') {
        try {
            return Math.min( obj, params.max_cpus as int )
        } catch (all) {
            println "   ### ERROR ###   Max cpus '${params.max_cpus}' is not valid! Using default value: $obj"
            return obj
        }
    }
}
